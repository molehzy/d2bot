.版本 2
.支持库 CommonSupport

.程序集 Lib
.子程序 初始化
    应用目录 ＝ 取运行目录 () ＋ “\D2Bot\”
    日志目录 ＝ 取运行目录 () ＋ “\log\”
    缓存目录 ＝ 应用目录 ＋ “temp\”
    配置文件 ＝ 应用目录 ＋ “Config.ini”
    群配置文件 ＝ 应用目录 ＋ “Group.ini”
    绑定信息文件 ＝ 应用目录 ＋ “BD.db”
    词库信息文件 ＝ 应用目录 ＋ “CK.db”
    CQ.初始化 (&private, &group, &notice, &request, &ws)
    日志 (日志.初始化 (日志目录))
    日志 (“===启动”)
    日志 (“消息线程池: ” ＋ 到文本 (线程池_消息.创建 (&消息处理, , 2, 4)))
    
    .如果真 (目录_是否存在 (应用目录) ＝ 假)
        目录_创建 (应用目录)
    .如果真结束
    .如果真 (文件_是否存在 (绑定信息文件) ＝ 假)
        写到文件 (绑定信息文件, { })
    .如果真结束
    .如果真 (文件_是否存在 (词库信息文件) ＝ 假)
        写到文件 (词库信息文件, { })
    .如果真结束
    .如果真 (文件_是否存在 (配置文件) ＝ 假)
        写配置项 (配置文件, “CFG”, “WS”, “127.0.0.1:6700”)
        写配置项 (配置文件, “CFG”, “Token”, “”)
        写配置项 (配置文件, “CFG”, “HTTP”, “127.0.0.1:5700”)
        写配置项 (配置文件, “D2”, “X-API-KEY”, “”)
        写配置项 (配置文件, “D2”, “HTTP代理”, “”)
        写配置项 (配置文件, “D2”, “启用代理”, “假”)
        写配置项 (配置文件, “BOT”, “突袭”, “D2突袭(46\d+)|D2突袭(7656\d+)|D2突袭”)
        写配置项 (配置文件, “BOT”, “地牢”, “D2地牢(46\d+)|D2地牢(7656\d+)|D2地牢”)
        写配置项 (配置文件, “BOT”, “Steam搜索”, “D2搜索(7656\d+)”)
        写配置项 (配置文件, “BOT”, “历史统计”, “D2统计(46\d+)|D2统计(7656\d+)|D2统计”)
        写配置项 (配置文件, “BOT”, “角色”, “D2角色(46\d+)|D2角色(7656\d+)|D2角色”)
        写配置项 (配置文件, “BOT”, “ELO”, “D2elo(46\d+)|D2elo(7656\d+)|D2elo”)
        写配置项 (配置文件, “BOT”, “绑定”, “d2绑定(7656(\d+))”)
        写配置项 (配置文件, “BOT”, “读绑定”, “/id”)
        
    .如果真结束
    
    词库_初始化 ()
    绑定_初始化 ()
    配置刷新 ()
    

.子程序 读配置项_逻辑型, 逻辑型
    .参数 文件名, 文本型
    .参数 配置节, 文本型
    .参数 配置项, 文本型
    .参数 默认, 逻辑型

    .判断开始 (读配置项 (文件名, 配置节, 配置项, ) ＝ “”)
        返回 (默认)
    .判断 (读配置项 (文件名, 配置节, 配置项, ) ＝ “真”)
        返回 (真)
    .默认
        返回 (假)
    .判断结束
    

.子程序 取时间_自定义, 文本型
    返回 (时间_到文本 (, 2, 2) ＋ “ ” ＋ 时间_到文本 (, 1, 3) ＋ “』 ”)
    

.子程序 常量替换, 文本型
    .参数 文本, 文本型

    返回 (文本_替换 (文本, , , 真, #换行符, “#换行符”, “ ”, “#空格”))
    

.子程序 常量恢复, 文本型
    .参数 文本, 文本型

    返回 (文本_替换 (文本, , , 真, “#换行符”, #换行符, “#空格”, “ ”))
    

.子程序 D2_JSON转北京时间, 文本型
    .参数 时间, 文本型
    .局部变量 已分割, 文本型, , "0"

    已分割 ＝ 分割文本 (时间, “T”, )
    返回 (时间_时间戳转文本 (到文本 (到长整数 (时间_到时间戳 (到时间 (已分割 [1] ＋ “ ” ＋ 文本_删右边 (已分割 [2], 1)), 真)) ＋ 28800)))
    

.子程序 D2_分钟转时间, 文本型
    .参数 分, 整数型

    返回 (时间_秒到时分秒格式 (分 × 60, “h时m分”, 真))
    

.子程序 D2_计算百分比, 文本型
    .参数 除数, 文本型
    .参数 被除数, 文本型

    返回 (数值到格式文本 (到整数 (被除数) ÷ 到整数 (除数) × 100, 2, 假) ＋ “%”)
    

.子程序 D2_PVP活动翻译, 文本型
    .参数 名称, 文本型

    .判断开始 (名称 ＝ “Crimson Doubles”)
        返回 (“血色浪漫”)
    .判断 (名称 ＝ “Iron Banner”)
        返回 (“铁旗”)
    .判断 (名称 ＝ “Mayhem”)
        返回 (“鏖战”)
    .判断 (名称 ＝ “Survival”)
        返回 (“生存”)
    .判断 (名称 ＝ “Rumble”)
        返回 (“混战”)
    .判断 (名称 ＝ “Gambit”)
        返回 (“智谋”)
    .判断 (名称 ＝ “Gambit Prime”)
        返回 (“全盛智谋”)
    .判断 (名称 ＝ “Elimination”)
        返回 (“灭绝”)
    .判断 (名称 ＝ “Trials of Osiris”)
        返回 (“奥斯里斯试炼”)
    .判断 (名称 ＝ “Control: Quickplay”)
        返回 (“占领:匹配”)
    .判断 (名称 ＝ “Supremacy”)
        返回 (“霸权”)
    .判断 (名称 ＝ “Clash: Quickplay”)
        返回 (“死斗:匹配”)
    .判断 (名称 ＝ “Countdown”)
        返回 (“争分夺秒”)
    .判断 (名称 ＝ “Showdown”)
        返回 (“较量”)
    .判断 (名称 ＝ “Lockdown”)
        返回 (“区域拒止”)
    .判断 (名称 ＝ “Scorched Team”)
        返回 (“焦灼小队”)
    .判断 (名称 ＝ “Breakthrough”)
        返回 (“突破”)
    .判断 (名称 ＝ “Salvage”)
        返回 (“救援”)
    .判断 (名称 ＝ “Momentum”)
        返回 (“蓄势待发”)
    .默认
        返回 (名称)
    .判断结束
    

.子程序 D2_前百分比, 文本型
    .参数 百分比, 双精度小数型

    返回 (到文本 (100 － 百分比) ＋ “%”)
    

.子程序 日志
    .参数 内容, 文本型

    日志.写_ (_启动窗口.编辑框_info, 取时间_自定义 () ＋ 内容)
    


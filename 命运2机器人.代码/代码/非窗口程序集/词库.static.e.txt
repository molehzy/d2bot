.版本 2
.支持库 CommonSupport
.支持库 e2ee

.程序集 词库
.子程序 词库_初始化
    词库许可证.创建 ()
    日志 (“词库载入: ” ＋ 到文本 (词库DB.从字节集载入 (LZMA解压数据 (读入文件 (词库信息文件)))))
    

.子程序 词库_添加
    .参数 类型, 文本型
    .参数 正则, 文本型
    .参数 内容, 文本型
    .局部变量 data, 存取键值表
    .局部变量 i, 整数型

    词库许可证.进入 ()
    data.创建 (“类型”, 类型, “正则”, 正则, “内容”, 内容)
    .计次循环首 (词库DB.取数量 (), i)
        
        .如果真 (正则 ＝ 词库DB.取文本 (“//” ＋ 到文本 (i) ＋ “.正则”))
            词库DB.置键值表 (“//” ＋ 到文本 (i), data, )
            写到文件 (词库信息文件, LZMA压缩数据 (词库DB.到字节集 (真), 9))
            词库许可证.退出 ()
            词库GUI_刷新列表框 ()
            返回 ()
        .如果真结束
        
    .计次循环尾 ()
    词库DB.置键值表 (“//” ＋ 到文本 (词库DB.取数量 () ＋ 1), data, )
    写到文件 (词库信息文件, LZMA压缩数据 (词库DB.到字节集 (真), 9))
    词库许可证.退出 ()
    词库GUI_刷新列表框 ()
    

.子程序 词库_删除
    .参数 主键, 文本型
    .局部变量 data, 存取键值表
    .局部变量 data2, 存取键值表
    .局部变量 i, 整数型
    .局部变量 ii, 整数型

    词库许可证.进入 ()
    .计次循环首 (词库DB.取数量 (), i)
        .如果真 (主键 ＝ 到文本 (i))
            到循环尾 ()
        .如果真结束
        
        词库DB.取键值表 (“//” ＋ 到文本 (i), data)
        ii ＝ ii ＋ 1
        data2.置键值表 (到文本 (ii), data, )
    .计次循环尾 ()
    写到文件 (词库信息文件, LZMA压缩数据 (data2.到字节集 (真), 9))
    词库许可证.退出 ()
    词库GUI_刷新列表框 ()
    

.子程序 词库_读正则
    .参数 正则, 文本型, 参考 数组
    .局部变量 i, 整数型

    词库许可证.进入 ()
    .计次循环首 (词库DB.取数量 (), i)
        加入成员 (正则, 词库DB.取文本 (“//” ＋ 到文本 (i) ＋ “.正则”))
    .计次循环尾 ()
    词库许可证.退出 ()
    

.子程序 词库_读内容, 文本型, , 返回类型
    .参数 正则, 文本型
    .参数 内容, 文本型, 参考
    .局部变量 i, 整数型
    .局部变量 类型, 文本型

    词库许可证.进入 ()
    .计次循环首 (词库DB.取数量 (), i)
        .如果真 (正则 ＝ 词库DB.取文本 (“//” ＋ 到文本 (i) ＋ “.正则”))
            内容 ＝ 词库DB.取文本 (“//” ＋ 到文本 (i) ＋ “.内容”)
            类型 ＝ 词库DB.取文本 (“//” ＋ 到文本 (i) ＋ “.类型”)
            词库许可证.退出 ()
            返回 (类型)
        .如果真结束
        
    .计次循环尾 ()
    词库许可证.退出 ()
    返回 (“”)
    

.子程序 词库_读配置
    .参数 主键, 文本型
    .参数 类型, 文本型, 参考
    .参数 正则, 文本型, 参考
    .参数 内容, 文本型, 参考

    词库许可证.进入 ()
    类型 ＝ 词库DB.取文本 (“//” ＋ 到文本 (主键) ＋ “.类型”)
    正则 ＝ 词库DB.取文本 (“//” ＋ 到文本 (主键) ＋ “.正则”)
    内容 ＝ 词库DB.取文本 (“//” ＋ 到文本 (主键) ＋ “.内容”)
    词库许可证.退出 ()
    

